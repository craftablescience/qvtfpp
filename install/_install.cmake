# Set up install rules
install(TARGETS ${PROJECT_NAME}
		DESTINATION "${QT6_INSTALL_PLUGINS}/imageformats")

install(FILES "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE"
		DESTINATION "share/licenses/${PROJECT_NAME}")

install(FILES "${CMAKE_CURRENT_LIST_DIR}/mime-type.xml"
		DESTINATION "share/mime/packages"
		RENAME "${PROJECT_NAME}.xml")

# CPack stuff
set(CPACK_PACKAGE_NAME ${PROJECT_NAME_PRETTY})
set(CPACK_PACKAGE_VENDOR ${PROJECT_ORGANIZATION_NAME})
set(CPACK_PACKAGE_CONTACT "lauralewisdev@gmail.com")
set(CPACK_PACKAGE_DESCRIPTION ${CMAKE_PROJECT_DESCRIPTION})
set(CPACK_PACKAGE_DESCRIPTION_SUMMARY ${CPACK_PACKAGE_DESCRIPTION})
set(CPACK_PACKAGE_INSTALL_DIRECTORY ${CPACK_PACKAGE_NAME})
set(CPACK_RESOURCE_FILE_LICENSE "${CMAKE_CURRENT_SOURCE_DIR}/LICENSE")
set(CPACK_RESOURCE_FILE_README "${CMAKE_CURRENT_SOURCE_DIR}/README.md")
set(CPACK_MONOLITHIC_INSTALL ON)
set(CPACK_PACKAGE_EXECUTABLES ${PROJECT_NAME} ${PROJECT_NAME_PRETTY})
set(CPACK_STRIP_FILES ON)
set(CPACK_THREADS 0)
set(CMAKE_INSTALL_DEFAULT_DIRECTORY_PERMISSIONS OWNER_READ OWNER_WRITE OWNER_EXECUTE GROUP_READ GROUP_EXECUTE WORLD_READ WORLD_EXECUTE)
if(CPACK_GENERATOR STREQUAL "DEB")
	set(CPACK_DEBIAN_PACKAGE_MAINTAINER "${CPACK_PACKAGE_VENDOR} <${CPACK_PACKAGE_CONTACT}>")
	set(CPACK_DEBIAN_PACKAGE_DEPENDS "libqt6gui6 (>= 6)")
	set(CPACK_DEBIAN_COMPRESSION_TYPE "zstd")
elseif(CPACK_GENERATOR STREQUAL "RPM")
	set(CPACK_RPM_PACKAGE_LICENSE "LGPL-2.1-or-later")
	set(CPACK_RPM_PACKAGE_REQUIRES "qt6-qtbase-gui >= 6")
	if(CMAKE_VERSION VERSION_LESS "3.31")
		set(CPACK_RPM_COMPRESSION_TYPE "xz")
	else()
		set(CPACK_RPM_COMPRESSION_TYPE "zstd")
	endif()
else()
	message(FATAL_ERROR "CPACK_GENERATOR is unset! Only DEB and RPM generators are supported.")
endif()
include(CPack)
